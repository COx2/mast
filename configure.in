
dnl Require autoconf version >= 2.57
AC_PREREQ(2.57)


dnl ############# Initialisation

AC_INIT([mast], [0.2a], [njh@ecs.soton.ac.uk])

AC_CONFIG_SRCDIR( src/mastcast.c )
AC_CONFIG_AUX_DIR( build) 
AC_CANONICAL_SYSTEM

dnl Version 1.7 of automake is recommended
AM_INIT_AUTOMAKE( 1.7 )
AM_CONFIG_HEADER( build/config.h )



dnl ############# Compiler and tools Checks

AC_PROG_CC
AC_PROG_INSTALL
AC_PROG_LN_S
AC_C_BIGENDIAN
AC_C_INLINE



dnl ############# Configure Arguments

AC_ARG_ENABLE(debug,
              [--enable-debug=[no/yes] turn on debugging],
              [ Debugging="Enabled" 
                AC_DEFINE(DEBUG, 1, [ Define if debugging is enabled. ]) ],
              [ Debugging="Disabled" ]
)



dnl ############## Check for tools to build documentation

AC_PATH_PROG( [POD2MAN], [pod2man] )
AC_SUBST( POD2MAN )

AC_PATH_PROG( [POD2TEXT], [pod2text]  )
AC_SUBST( POD2TEXT )


dnl ############## Library Checks

dnl Check for libsndfile
PKG_CHECK_MODULES(SNDFILE, sndfile >= 1.0.0,
	[ sndfileSupport="Enabled" 
	  AC_DEFINE(HAVE_SNDFILE, 1, [ Define if libsndfile v1.X is available ]) ],
	[ sndfileSupport="Disabled" ]
)

dnl Check for JACK
PKG_CHECK_MODULES(JACK, jack,
	[ JACKsupport="Enabled"
	  AC_DEFINE(HAVE_JACK, 1, [ Define if JACK audio server is available ]) ],
	[ JACKsupport="Disabled" ]
)

dnl Check for ortp
PKG_CHECK_MODULES(ORTP, ortp >= 0.7.1, ,
	[ AC_MSG_ERROR([ Version 1.7.1 or greater of the ortp library is required to build MAST. It can be downloaded from http://www.linphone.org/ortp/ ]) ]
)



dnl ############## Header Checks

AC_HEADER_STDC
dnl AC_CHECK_HEADERS(standards.h signal.h)
dnl AC_CHECK_HEADERS(string.h strings.h sys/resource.h sys/types.h unistd.h)
dnl AC_CHECK_HEADERS(sys/bitypes.h sys/socket.h)

dnl Check for Linux (OSS) sound support
AC_CHECK_HEADER(sys/soundcard.h, 
		[ OSSsupport="Enabled"
		  AC_DEFINE(HAVE_OSS, 1, [ Define if OSS sound support is available ]) ],
		[ OSSsupport="Disabled" ]
)



dnl ############## Type checks

AC_CHECK_TYPE(u_int32_t, unsigned long)
AC_CHECK_TYPE(u_int16_t, unsigned short)
AC_CHECK_TYPE(u_int8_t, unsigned char)



dnl ############## Compiler and Linker Flags
CFLAGS="$CFLAGS $ORTP_CFLAGS -Wunused -Wall "
LDFLAGS="$LDFLAGS $ORTP_LIBS "

if test x$Debugging = xEnabled; then
	CFLAGS="$CFLAGS -g -Werror"
fi



dnl ############## Final Output

AC_OUTPUT(Makefile src/Makefile doc/Makefile)



dnl ############## Display Message

echo "
  Multicast Audio Streaming Toolkit $VERSION
  Copyright (C) 2003-2005 University of Southampton

  Install path .......... $prefix
  Debugging ............. $Debugging
  OSS support ........... $OSSsupport
  JACK support .......... $JACKsupport
  libsndfile support .... $sndfileSupport
  
Next type 'make' and then 'make install'."

